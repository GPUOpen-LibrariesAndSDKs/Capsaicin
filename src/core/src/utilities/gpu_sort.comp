/**********************************************************************
Copyright (c) 2024 Advanced Micro Devices, Inc. All rights reserved.

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in
all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.  IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
THE SOFTWARE.
********************************************************************/

#include "gpu_shared.h"

#ifndef TYPE
#define TYPE float
#endif
#ifndef OP
#define OP 0
#endif

#define FFX_GPU 1
#define FFX_HLSL 1
#define FFX_HLSL_SM 67
#include "FidelityFX/gpu/ffx_core.h"

struct FfxParallelSortConstants
{
    FfxUInt32 numKeys; ///< The number of keys to sort
    FfxInt32 numBlocksPerThreadGroup; ///< How many blocks of keys each thread group needs to process
    FfxUInt32 numThreadGroups; ///< How many thread groups are being run concurrently for sort
    FfxUInt32 numThreadGroupsWithAdditionalBlocks; ///< How many thread groups need to process additional block data
    FfxUInt32 numReduceThreadgroupPerBin; ///< How many thread groups are summed together for each reduced bin entry
    FfxUInt32 numScanValues; ///< How many values to perform scan prefix (+ add) on
    FfxUInt32 shift; ///< What bits are being sorted (4 bit increments)
    FfxUInt32 padding; ///< Padding - unused

};

StructuredBuffer<FfxParallelSortConstants> CBuffer; // Constant buffer
uint CShiftBit;

RWStructuredBuffer<uint> SrcBuffer; // The unsorted keys or scan data
RWStructuredBuffer<uint> SrcPayload; // The payload data

RWStructuredBuffer<uint> SumTable; // The sum table we will write sums to
RWStructuredBuffer<uint> ReduceTable; // The reduced sum table we will write sums to

RWStructuredBuffer<uint> DstBuffer; // The sorted keys or prefixed data
RWStructuredBuffer<uint> DstPayload; // The sorted payload data

RWStructuredBuffer<uint> ScanSrc; // Source for Scan Data
RWStructuredBuffer<uint> ScanDst; // Destination for Scan Data
RWStructuredBuffer<uint> ScanScratch; // Scratch data for Scan

StructuredBuffer<uint> numKeys; // Number of keys to sort for indirect execution
RWStructuredBuffer<FfxParallelSortConstants> CBufferUAV; // UAV for constant buffer parameters for indirect execution
RWStructuredBuffer<uint> CountScatterArgs; // Count and Scatter Args for indirect execution
RWStructuredBuffer<uint> ReduceScanArgs; // Reduce and Scan Args for indirect execution


FfxUInt32 FfxNumBlocksPerThreadGroup()
{
    return CBuffer[0].numBlocksPerThreadGroup;
}

FfxUInt32 FfxNumThreadGroups()
{
    return CBuffer[0].numThreadGroups;
}

FfxUInt32 FfxNumThreadGroupsWithAdditionalBlocks()
{
    return CBuffer[0].numThreadGroupsWithAdditionalBlocks;
}

FfxUInt32 FfxNumReduceThreadgroupPerBin()
{
    return CBuffer[0].numReduceThreadgroupPerBin;
}

FfxUInt32 FfxNumKeys()
{
    return CBuffer[0].numKeys;
}

FfxUInt32 FfxLoadKey(FfxUInt32 index)
{
    return SrcBuffer[index];
}

void FfxStoreKey(FfxUInt32 index, FfxUInt32 value)
{
    DstBuffer[index] = value;
}

FfxUInt32 FfxLoadPayload(FfxUInt32 index)
{
    return SrcPayload[index];
}

void FfxStorePayload(FfxUInt32 index, FfxUInt32 value)
{
    DstPayload[index] = value;
}

FfxUInt32 FfxLoadSum(FfxUInt32 index)
{
    return SumTable[index];
}

void FfxStoreSum(FfxUInt32 index, FfxUInt32 value)
{
    SumTable[index] = value;
}

void FfxStoreReduce(FfxUInt32 index, FfxUInt32 value)
{
    ReduceTable[index] = value;
}

FfxUInt32 FfxLoadScanSource(FfxUInt32 index)
{
    return ScanSrc[index];
}

void FfxStoreScanDest(FfxUInt32 index, FfxUInt32 value)
{
    ScanDst[index] = value;
}

FfxUInt32 FfxLoadScanScratch(FfxUInt32 index)
{
    return ScanScratch[index];
}

#if OP==1
#define FFX_PARALLELSORT_SORT_DESCENDING
#endif
#include "FidelityFX/gpu/parallelsort/ffx_parallelsort.h"
// Need to include ffx-parallelsort twice to get 2 different versions (with and without payload)
namespace Payload
{
#define FFX_PARALLELSORT_COPY_VALUE
#include "FidelityFX/gpu/parallelsort/ffx_parallelsort.h"
#undef FFX_PARALLELSORT_COPY_VALUE
}

[numthreads(1, 1, 1)]
void setupIndirectParameters(uint localID : SV_GroupThreadID)
{
    CBufferUAV[0].numKeys = numKeys[0];
    uint BlockSize = FFX_PARALLELSORT_ELEMENTS_PER_THREAD * FFX_PARALLELSORT_THREADGROUP_SIZE;
    uint NumBlocks = (numKeys[0] + BlockSize - 1) / BlockSize;
    // Figure out data distribution
    uint NumThreadGroupsToRun = 800;
    uint BlocksPerThreadGroup = (NumBlocks / NumThreadGroupsToRun);
    CBufferUAV[0].numThreadGroupsWithAdditionalBlocks = NumBlocks % NumThreadGroupsToRun;
    if (NumBlocks < NumThreadGroupsToRun)
    {
        BlocksPerThreadGroup = 1;
        NumThreadGroupsToRun = NumBlocks;
        CBufferUAV[0].numThreadGroupsWithAdditionalBlocks = 0;
    }
    CBufferUAV[0].numThreadGroups = NumThreadGroupsToRun;
    CBufferUAV[0].numBlocksPerThreadGroup = BlocksPerThreadGroup;
    // Calculate the number of thread groups to run for reduction (each thread group can process BlockSize number of entries)
    uint NumReducedThreadGroupsToRun = FFX_PARALLELSORT_SORT_BIN_COUNT * ((BlockSize > NumThreadGroupsToRun) ? 1 : (NumThreadGroupsToRun + BlockSize - 1) / BlockSize);
    CBufferUAV[0].numReduceThreadgroupPerBin = NumReducedThreadGroupsToRun / FFX_PARALLELSORT_SORT_BIN_COUNT;
    CBufferUAV[0].numScanValues = NumReducedThreadGroupsToRun; // The number of reduce thread groups becomes our scan count (as each thread group writes out 1 value that needs scan prefix)
    // Setup dispatch arguments
    CountScatterArgs[0] = NumThreadGroupsToRun;
    CountScatterArgs[1] = 1;
    CountScatterArgs[2] = 1;
    ReduceScanArgs[0] = NumReducedThreadGroupsToRun;
    ReduceScanArgs[1] = 1;
    ReduceScanArgs[2] = 1;
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void count(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    // Call the uint version of the count part of the algorithm
    ffxParallelSortCountUInt(localID, groupID, CShiftBit);
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void countReduce(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    // Call the reduce part of the algorithm
    ffxParallelSortReduceCount(localID, groupID);
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void scan(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    uint BaseIndex = FFX_PARALLELSORT_ELEMENTS_PER_THREAD * FFX_PARALLELSORT_THREADGROUP_SIZE * groupID;
    ffxParallelSortScanPrefix(CBuffer[0].numScanValues, localID, groupID, 0, BaseIndex, false);
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void scanAdd(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    // When doing adds, we need to access data differently because reduce
    // has a more specialized access pattern to match optimized count
    // Access needs to be done similarly to reduce
    // Figure out what bin data we are reducing
    uint BinID = groupID / CBuffer[0].numReduceThreadgroupPerBin;
    uint BinOffset = BinID * CBuffer[0].numThreadGroups;

    // Get the base index for this thread group
    uint BaseIndex = (groupID % CBuffer[0].numReduceThreadgroupPerBin) * FFX_PARALLELSORT_ELEMENTS_PER_THREAD * FFX_PARALLELSORT_THREADGROUP_SIZE;

    ffxParallelSortScanPrefix(CBuffer[0].numThreadGroups, localID, groupID, BinOffset, BaseIndex, true);
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void scatter(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    ffxParallelSortScatterUInt(localID, groupID, CShiftBit);
}

[numthreads(FFX_PARALLELSORT_THREADGROUP_SIZE, 1, 1)]
void scatterPayload(uint localID : SV_GroupThreadID, uint groupID : SV_GroupID)
{
    Payload::ffxParallelSortScatterUInt(localID, groupID, CShiftBit);
}
